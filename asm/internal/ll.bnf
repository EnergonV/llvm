// ### [ Lexical part ] ########################################################

_ascii_letter
	: 'A' - 'Z'
	| 'a' - 'z'
;

_letter
	: _ascii_letter
	| '$'
	| '-'
	| '.'
	| '_'
;

_decimal_digit
	: '0' - '9'
;

_hex_digit
	: '0' - '9'
	| 'A' - 'F'
	| 'a' - 'f'
;

!comment : ';' { . } '\n' ;

!whitespace : '\x00' | ' ' | '\t' | '\r' | '\n' ;

// === [ Identifiers ] =========================================================

_name
	: _letter { _letter | _decimal_digit }
;

_quoted_name
	: _quoted_string
;

_id
	: _decimals
;

// --- [ Global identifiers ] --------------------------------------------------

global_ident
	: _global_name
	| _global_id
;

_global_name
	: '@' ( _name | _quoted_name )
;

_global_id
	: '@' _id
;

// --- [ Local identifiers ] ---------------------------------------------------

local_ident
	: _local_name
	| _local_id
;

_local_name
	: '%' ( _name | _quoted_name )
;

_local_id
	: '%' _id
;

// --- [ Labels ] --------------------------------------------------------------

//   Label             [-a-zA-Z$._0-9]+:

label_ident
	: ( _letter | _decimal_digit ) { _letter | _decimal_digit } ':'
	| _quoted_string ':'
;

// === [ Integer literals ] ====================================================

//   Integer           [-]?[0-9]+

int_lit
	: _decimal_lit
;

_decimal_lit
	: [ '-' ] _decimals
;

_decimals
	: _decimal_digit { _decimal_digit }
;

// === [ Float literals ] ====================================================

//   FPConstant        [-+]?[0-9]+[.][0-9]*([eE][-+]?[0-9]+)?

float_lit
	: _frac_lit
	| _sci_lit
	| _float_hex_lit
;

_frac_lit
	: ( '+' | '-' ) _decimals '.' _decimals
	| _decimals '.' _decimals
;

_sci_lit
	: _frac_lit ( 'e' | 'E' ) ( '+' | '-' ) _decimals
	| _frac_lit ( 'e' | 'E' ) _decimals
;

//   HexFPConstant     0x[0-9A-Fa-f]+     // 16 hex digits
//   HexFP80Constant   0xK[0-9A-Fa-f]+    // 20 hex digits
//   HexFP128Constant  0xL[0-9A-Fa-f]+    // 32 hex digits
//   HexPPC128Constant 0xM[0-9A-Fa-f]+    // 32 hex digits
//   HexHalfConstant   0xH[0-9A-Fa-f]+    // 4 hex digits

_float_hex_lit
	:  '0' 'x'      _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit
	|  '0' 'x' 'K'  _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit
	|  '0' 'x' 'L'  _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit
	|  '0' 'x' 'M'  _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit _hex_digit
	|  '0' 'x' 'H'  _hex_digit _hex_digit _hex_digit _hex_digit
;

// === [ String literals ] =====================================================

_quoted_string
	: '"' { . } '"'
;

string_lit
	: _quoted_string
;

// === [ Types ] ===============================================================

int_type
	: 'i' _decimals
;

// ### [ Syntactic part ] ######################################################

<< import (
	"github.com/llir/llvm/asm/internal/ast"
	"github.com/llir/llvm/asm/internal/astx"
) >>

// === [ Modules ] =============================================================

Module
	: TopLevelDecls   << astx.NewModule($0) >>
;

TopLevelDecls
	: empty
	| TopLevelDeclList
;

TopLevelDeclList
	: TopLevelDecl                    << astx.NewTopLevelDeclList($0) >>
	| TopLevelDeclList TopLevelDecl   << astx.AppendTopLevelDecl($0, $1) >>
;

TopLevelDecl
	: TypeDef
	| Global
	| FunctionDecl
	| FunctionDef
;

// === [ Type definitions ] ====================================================

TypeDef
	: LocalIdent "=" "type" Type       << astx.NewTypeDef($0, $3) >>
	| LocalIdent "=" "type" "opaque"   << astx.NewTypeDef($0, nil) >>
;

// === [ Global variables ] ====================================================

Global
	: GlobalIdent "=" "external" Immutable Type   << astx.NewGlobalDecl($0, $3, $4) >>
	| GlobalIdent "=" Immutable Type Constant     << astx.NewGlobalDef($0, $2, $3, $4) >>
;

Immutable
	: "constant"   << true, nil >>
	| "global"     << false, nil >>
;

// === [ Functions ] ===========================================================

FunctionDecl
	: "declare" FunctionHeader   << $1, nil >>
;

FunctionDef
	: "define" FunctionHeader FunctionBody   << astx.NewFunctionDef($1, $2) >>
;

FunctionHeader
	: ResultType GlobalIdent "(" Params ")"   << astx.NewFunctionDecl($0, $1, $3) >>
;

Params
	: empty
	| "..."                 << astx.NewParams(nil, true) >>
	| ParamList             << astx.NewParams($0, false) >>
	| ParamList "," "..."   << astx.NewParams($0, true) >>
;

ParamList
	: Param                 << astx.NewParamList($0) >>
	| ParamList "," Param   << astx.AppendParam($0, $2) >>
;

Param
	: Type              << astx.NewParam($0, nil) >>
	| Type LocalIdent   << astx.NewParam($0, $1) >>
;

FunctionBody
	: "{" BasicBlocks "}"   << $1, nil >>
;

// === [ Identifiers ] =========================================================

GlobalIdent
	: global_ident   << astx.NewGlobalIdent($0) >>
;

LocalIdent
	: local_ident   << astx.NewLocalIdent($0) >>
;

LabelIdent
	: label_ident   << astx.NewLabelIdent($0) >>
;

// === [ Types ] ===============================================================

Type
	: LabelType
	| IntType
	| FloatType
	| FuncType
	| PointerType
	| VectorType
	| ArrayType
	| StructType
	| NamedType
;

ResultType
	: VoidType
	| Type
;

VoidType
	: "void"   << &ast.VoidType{}, nil >>
;

LabelType
	: "label"   << &ast.LabelType{}, nil >>
;

IntType
	: int_type   << astx.NewIntType($0) >>
;

FloatType
	: "half"        << &ast.FloatType{Kind: ast.FloatKindIEEE_16}, nil >>
	| "float"       << &ast.FloatType{Kind: ast.FloatKindIEEE_32}, nil >>
	| "double"      << &ast.FloatType{Kind: ast.FloatKindIEEE_64}, nil >>
	| "fp128"       << &ast.FloatType{Kind: ast.FloatKindIEEE_128}, nil >>
	| "x86_fp80"    << &ast.FloatType{Kind: ast.FloatKindDoubleExtended_80}, nil >>
	| "ppc_fp128"   << &ast.FloatType{Kind: ast.FloatKindDoubleDouble_128}, nil >>
;

FuncType
	: ResultType "(" Params ")"   << astx.NewFuncType($0, $2) >>
;

PointerType
	: Type OptAddrSpace "*"   << astx.NewPointerType($0, $1) >>
;

OptAddrSpace
	: empty
	| "addrspace" "(" IntConst ")"   << astx.NewAddrSpace($2) >>
;

VectorType
	: "<" IntConst "x" Type ">"   << astx.NewVectorType($1, $3) >>
;

ArrayType
	: "[" IntConst "x" Type "]"   << astx.NewArrayType($1, $3) >>
;

StructType
	: "{" Fields "}"   << astx.NewStructType($1) >>
;

Fields
	: empty
	| FieldList
;

FieldList
	: Type                 << astx.NewTypeList($0) >>
	| FieldList "," Type   << astx.AppendType($0, $2) >>
;

NamedType
	: LocalIdent   << astx.NewTypeIdent($0) >>
;

// === [ Values ] ==============================================================

Value
	: LocalIdent
	| Constant
;

// === [ Constants ] ===========================================================

Constant
	: IntConst
	| FloatConst
	| NullConst
	| VectorConst
	| ArrayConst
	| CharArrayConst
	| StructConst
	| ZeroInitializerConst
	| GlobalIdent
	| ConstExpr
;

IntConst
	: int_lit   << astx.NewIntLit($0) >>
	| "true"    << astx.NewIntLit($0) >>
	| "false"   << astx.NewIntLit($0) >>
;

FloatConst
	: float_lit   << astx.NewFloatLit($0) >>
;

NullConst
	: "null"   << &astx.NullLit{}, nil >>
;

VectorConst
	: "<" Elems ">"   << astx.NewVectorConst($1) >>
;

ArrayConst
	: "[" Elems "]"   << astx.NewArrayConst($1) >>
;

Elems
	: empty
	| ElemList
;

ElemList
	: Elem                << astx.NewConstantList($0) >>
	| ElemList "," Elem   << astx.AppendConstant($0, $2) >>
;

Elem
	: Type Value   << astx.NewConstant($0, $1) >>
;

CharArrayConst
	: "c" string_lit   << astx.NewCharArrayConst($1) >>
;

StructConst
	: "{" Elems "}"   << astx.NewStructConst($1) >>
;

ZeroInitializerConst
	: "zeroinitializer"   << &astx.ZeroInitializerLit{}, nil >>
;

ConstExpr
	// Binary expressions
	: AddExpr
	| FAddExpr
	| SubExpr
	| FSubExpr
	| MulExpr
	| FMulExpr
	| UDivExpr
	| SDivExpr
	| FDivExpr
	| URemExpr
	| SRemExpr
	| FRemExpr
	// Bitwise expressions
	| ShlExpr
	| LShrExpr
	| AShrExpr
	| AndExpr
	| OrExpr
	| XorExpr
	// Memory expressions
	| GetElementPtrExpr
	// Conversion expressions
	| TruncExpr
	| ZExtExpr
	| SExtExpr
	| FPTruncExpr
	| FPExtExpr
	| FPToUIExpr
	| FPToSIExpr
	| UIToFPExpr
	| SIToFPExpr
	| PtrToIntExpr
	| IntToPtrExpr
	| BitCastExpr
	| AddrSpaceCastExpr
	// Other expressions
	| ICmpExpr
	| FCmpExpr
	| SelectExpr
;

// --- [ Binary expressions ] --------------------------------------------------

AddExpr
	: "add" "(" Type Constant "," Type Constant ")"   << astx.NewAddExpr($2, $3, $5, $6) >>
;

FAddExpr
	: "fadd" "(" Type Constant "," Type Constant ")"   << astx.NewFAddExpr($2, $3, $5, $6) >>
;

SubExpr
	: "sub" "(" Type Constant "," Type Constant ")"   << astx.NewSubExpr($2, $3, $5, $6) >>
;

FSubExpr
	: "fsub" "(" Type Constant "," Type Constant ")"   << astx.NewFSubExpr($2, $3, $5, $6) >>
;

MulExpr
	: "mul" "(" Type Constant "," Type Constant ")"   << astx.NewMulExpr($2, $3, $5, $6) >>
;

FMulExpr
	: "fmul" "(" Type Constant "," Type Constant ")"   << astx.NewFMulExpr($2, $3, $5, $6) >>
;

UDivExpr
	: "udiv" "(" Type Constant "," Type Constant ")"   << astx.NewUDivExpr($2, $3, $5, $6) >>
;

SDivExpr
	: "sdiv" "(" Type Constant "," Type Constant ")"   << astx.NewSDivExpr($2, $3, $5, $6) >>
;

FDivExpr
	: "fdiv" "(" Type Constant "," Type Constant ")"   << astx.NewFDivExpr($2, $3, $5, $6) >>
;

URemExpr
	: "urem" "(" Type Constant "," Type Constant ")"   << astx.NewURemExpr($2, $3, $5, $6) >>
;

SRemExpr
	: "srem" "(" Type Constant "," Type Constant ")"   << astx.NewSRemExpr($2, $3, $5, $6) >>
;

FRemExpr
	: "frem" "(" Type Constant "," Type Constant ")"   << astx.NewFRemExpr($2, $3, $5, $6) >>
;

// --- [ Bitwise expressions ] -------------------------------------------------

ShlExpr
	: "shl" "(" Type Constant "," Type Constant ")"   << astx.NewShlExpr($2, $3, $5, $6) >>
;

LShrExpr
	: "lshr" "(" Type Constant "," Type Constant ")"   << astx.NewLShrExpr($2, $3, $5, $6) >>
;

AShrExpr
	: "ashr" "(" Type Constant "," Type Constant ")"   << astx.NewAShrExpr($2, $3, $5, $6) >>
;

AndExpr
	: "and" "(" Type Constant "," Type Constant ")"   << astx.NewAndExpr($2, $3, $5, $6) >>
;

OrExpr
	: "or" "(" Type Constant "," Type Constant ")"   << astx.NewOrExpr($2, $3, $5, $6) >>
;

XorExpr
	: "xor" "(" Type Constant "," Type Constant ")"   << astx.NewXorExpr($2, $3, $5, $6) >>
;

// --- [ Memory expressions ] --------------------------------------------------

GetElementPtrExpr
	: "getelementptr" "(" Type "," Type Constant ConstIndices ")"   << astx.NewGetElementPtrExpr($2, $4, $5, $6) >>
;

ConstIndices
	: empty
	| "," ConstIndexList   << $1, nil >>
;

ConstIndexList
	: ConstIndex                      << astx.NewConstantList($0) >>
	| ConstIndexList "," ConstIndex   << astx.AppendConstant($0, $2) >>
;

ConstIndex
	: IntType Constant   << astx.NewConstant($0, $1) >>
;

// --- [ Conversion expressions ] ----------------------------------------------

TruncExpr
	: "trunc" "(" Type Constant "to" Type ")"   << astx.NewTruncExpr($2, $3, $5) >>
;

ZExtExpr
	: "zext" "(" Type Constant "to" Type ")"   << astx.NewZExtExpr($2, $3, $5) >>
;

SExtExpr
	: "sext" "(" Type Constant "to" Type ")"   << astx.NewSExtExpr($2, $3, $5) >>
;

FPTruncExpr
	: "fptrunc" "(" Type Constant "to" Type ")"   << astx.NewFPTruncExpr($2, $3, $5) >>
;

FPExtExpr
	: "fpext" "(" Type Constant "to" Type ")"   << astx.NewFPExtExpr($2, $3, $5) >>
;

FPToUIExpr
	: "fptoui" "(" Type Constant "to" Type ")"   << astx.NewFPToUIExpr($2, $3, $5) >>
;

FPToSIExpr
	: "fptosi" "(" Type Constant "to" Type ")"   << astx.NewFPToSIExpr($2, $3, $5) >>
;

UIToFPExpr
	: "uitofp" "(" Type Constant "to" Type ")"   << astx.NewUIToFPExpr($2, $3, $5) >>
;

SIToFPExpr
	: "sitofp" "(" Type Constant "to" Type ")"   << astx.NewSIToFPExpr($2, $3, $5) >>
;

PtrToIntExpr
	: "ptrtoint" "(" Type Constant "to" Type ")"   << astx.NewPtrToIntExpr($2, $3, $5) >>
;

IntToPtrExpr
	: "inttoptr" "(" Type Constant "to" Type ")"   << astx.NewIntToPtrExpr($2, $3, $5) >>
;

BitCastExpr
	: "bitcast" "(" Type Constant "to" Type ")"   << astx.NewBitCastExpr($2, $3, $5) >>
;

AddrSpaceCastExpr
	: "addrspacecast" "(" Type Constant "to" Type ")"   << astx.NewAddrSpaceCastExpr($2, $3, $5) >>
;

// --- [ Other expressions ] ---------------------------------------------------

ICmpExpr
	: "icmp" ConstIntPred "(" Type Constant "," Type Constant ")"   << astx.NewICmpExpr($1, $3, $4, $6, $7) >>
;

ConstIntPred
	: "eq"    << ast.IntEQ, nil >>
	| "ne"    << ast.IntNE, nil >>
	| "ugt"   << ast.IntUGT, nil >>
	| "uge"   << ast.IntUGE, nil >>
	| "ult"   << ast.IntULT, nil >>
	| "ule"   << ast.IntULE, nil >>
	| "sgt"   << ast.IntSGT, nil >>
	| "sge"   << ast.IntSGE, nil >>
	| "slt"   << ast.IntSLT, nil >>
	| "sle"   << ast.IntSLE, nil >>
;

FCmpExpr
	: "fcmp" ConstFloatPred "(" Type Constant "," Type Constant ")"   << astx.NewFCmpExpr($1, $3, $4, $6, $7) >>
;

ConstFloatPred
	: "false"   << ast.FloatFalse, nil >>
	| "oeq"     << ast.FloatOEQ, nil >>
	| "ogt"     << ast.FloatOGT, nil >>
	| "oge"     << ast.FloatOGE, nil >>
	| "olt"     << ast.FloatOLT, nil >>
	| "ole"     << ast.FloatOLE, nil >>
	| "one"     << ast.FloatONE, nil >>
	| "ord"     << ast.FloatORD, nil >>
	| "ueq"     << ast.FloatUEQ, nil >>
	| "ugt"     << ast.FloatUGT, nil >>
	| "uge"     << ast.FloatUGE, nil >>
	| "ult"     << ast.FloatULT, nil >>
	| "ule"     << ast.FloatULE, nil >>
	| "une"     << ast.FloatUNE, nil >>
	| "uno"     << ast.FloatUNO, nil >>
	| "true"    << ast.FloatTrue, nil >>
;

SelectExpr
	: "select" "(" Type Constant "," Type Constant "," Type Constant ")"   << astx.NewSelectExpr($2, $3, $5, $6, $8, $9) >>
;

// === [ Basic blocks ] ========================================================

BasicBlocks
	: empty
	| BasicBlockList
;

BasicBlockList
	: BasicBlock                  << astx.NewBasicBlockList($0) >>
	| BasicBlockList BasicBlock   << astx.AppendBasicBlock($0, $1) >>
;

BasicBlock
	: Instructions Terminator              << astx.NewBasicBlock(nil, $0, $1) >>
	| LabelIdent Instructions Terminator   << astx.NewBasicBlock($0, $1, $2) >>
;

// === [ Instructions ] ========================================================

Instructions
	: empty
	| InstructionList
;

InstructionList
	: Instruction                   << astx.NewInstructionList($0) >>
	| InstructionList Instruction   << astx.AppendInstruction($0, $1) >>
;

Instruction
	: StoreInst
	| LocalIdent "=" ValueInstruction   << astx.NewNamedInstruction($0, $2) >>
	| ValueInstruction
;

ValueInstruction
	// Binary instructions
	: AddInst
	| FAddInst
	| SubInst
	| FSubInst
	| MulInst
	| FMulInst
	| UDivInst
	| SDivInst
	| FDivInst
	| URemInst
	| SRemInst
	| FRemInst
	// Bitwise instructions
	| ShlInst
	| LShrInst
	| AShrInst
	| AndInst
	| OrInst
	| XorInst
	// Memory instructions
	| AllocaInst
	| LoadInst
	| GetElementPtrInst
	// Conversion instructions
	| TruncInst
	| ZExtInst
	| SExtInst
	| FPTruncInst
	| FPExtInst
	| FPToUIInst
	| FPToSIInst
	| UIToFPInst
	| SIToFPInst
	| PtrToIntInst
	| IntToPtrInst
	| BitCastInst
	| AddrSpaceCastInst
	// Other instructions
	| ICmpInst
	| FCmpInst
	| PhiInst
	| SelectInst
	| CallInst
;

// --- [ Binary instructions ] -------------------------------------------------

AddInst
	: "add" Type Value "," Value   << astx.NewAddInst($1, $2, $4) >>
;

FAddInst
	: "fadd" Type Value "," Value   << astx.NewFAddInst($1, $2, $4) >>
;

SubInst
	: "sub" Type Value "," Value   << astx.NewSubInst($1, $2, $4) >>
;

FSubInst
	: "fsub" Type Value "," Value   << astx.NewFSubInst($1, $2, $4) >>
;

MulInst
	: "mul" Type Value "," Value   << astx.NewMulInst($1, $2, $4) >>
;

FMulInst
	: "fmul" Type Value "," Value   << astx.NewFMulInst($1, $2, $4) >>
;

UDivInst
	: "udiv" Type Value "," Value   << astx.NewUDivInst($1, $2, $4) >>
;

SDivInst
	: "sdiv" Type Value "," Value   << astx.NewSDivInst($1, $2, $4) >>
;

FDivInst
	: "fdiv" Type Value "," Value   << astx.NewFDivInst($1, $2, $4) >>
;

URemInst
	: "urem" Type Value "," Value   << astx.NewURemInst($1, $2, $4) >>
;

SRemInst
	: "srem" Type Value "," Value   << astx.NewSRemInst($1, $2, $4) >>
;

FRemInst
	: "frem" Type Value "," Value   << astx.NewFRemInst($1, $2, $4) >>
;

// --- [ Bitwise instructions ] ------------------------------------------------

ShlInst
	: "shl" Type Value "," Value   << astx.NewShlInst($1, $2, $4) >>
;

LShrInst
	: "lshr" Type Value "," Value   << astx.NewLShrInst($1, $2, $4) >>
;

AShrInst
	: "ashr" Type Value "," Value   << astx.NewAShrInst($1, $2, $4) >>
;

AndInst
	: "and" Type Value "," Value   << astx.NewAndInst($1, $2, $4) >>
;

OrInst
	: "or" Type Value "," Value   << astx.NewOrInst($1, $2, $4) >>
;

XorInst
	: "xor" Type Value "," Value   << astx.NewXorInst($1, $2, $4) >>
;

// --- [ Memory instructions ] -------------------------------------------------

AllocaInst
	: "alloca" Type OptNElems   << astx.NewAllocaInst($1, $2) >>
;

OptNElems
	: empty
	| "," Type Value   << astx.NewValue($1, $2) >>
;

LoadInst
	: "load" Type "," PointerType Value   << astx.NewLoadInst($1, $3, $4) >>
;

GetElementPtrInst
	: "getelementptr" Type "," Type Value Indices   << astx.NewGetElementPtrInst($1, $3, $4, $5) >>
;

Indices
	: empty
	| "," IndexList   << $1, nil >>
;

IndexList
	: Index                 << astx.NewValueList($0) >>
	| IndexList "," Index   << astx.AppendValue($0, $2) >>
;

Index
	: Type Value   << astx.NewValue($0, $1) >>
;

StoreInst
	: "store" Type Value "," PointerType Value   << astx.NewStoreInst($1, $2, $4, $5) >>
;

// --- [ Conversion instructions ] ---------------------------------------------

TruncInst
	: "trunc" Type Value "to" Type   << astx.NewTruncInst($1, $2, $4) >>
;

ZExtInst
	: "zext" Type Value "to" Type   << astx.NewZExtInst($1, $2, $4) >>
;

SExtInst
	: "sext" Type Value "to" Type   << astx.NewSExtInst($1, $2, $4) >>
;

FPTruncInst
	: "fptrunc" Type Value "to" Type   << astx.NewFPTruncInst($1, $2, $4) >>
;

FPExtInst
	: "fpext" Type Value "to" Type   << astx.NewFPExtInst($1, $2, $4) >>
;

FPToUIInst
	: "fptoui" Type Value "to" Type   << astx.NewFPToUIInst($1, $2, $4) >>
;

FPToSIInst
	: "fptosi" Type Value "to" Type   << astx.NewFPToSIInst($1, $2, $4) >>
;

UIToFPInst
	: "uitofp" Type Value "to" Type   << astx.NewUIToFPInst($1, $2, $4) >>
;

SIToFPInst
	: "sitofp" Type Value "to" Type   << astx.NewSIToFPInst($1, $2, $4) >>
;

PtrToIntInst
	: "ptrtoint" Type Value "to" Type   << astx.NewPtrToIntInst($1, $2, $4) >>
;

IntToPtrInst
	: "inttoptr" Type Value "to" Type   << astx.NewIntToPtrInst($1, $2, $4) >>
;

BitCastInst
	: "bitcast" Type Value "to" Type   << astx.NewBitCastInst($1, $2, $4) >>
;

AddrSpaceCastInst
	: "addrspacecast" Type Value "to" Type   << astx.NewAddrSpaceCastInst($1, $2, $4) >>
;

// --- [ Other instructions ] --------------------------------------------------

ICmpInst
	: "icmp" IntPred Type Value "," Value   << astx.NewICmpInst($1, $2, $3, $5) >>
;

IntPred
	: "eq"    << ast.IntEQ, nil >>
	| "ne"    << ast.IntNE, nil >>
	| "ugt"   << ast.IntUGT, nil >>
	| "uge"   << ast.IntUGE, nil >>
	| "ult"   << ast.IntULT, nil >>
	| "ule"   << ast.IntULE, nil >>
	| "sgt"   << ast.IntSGT, nil >>
	| "sge"   << ast.IntSGE, nil >>
	| "slt"   << ast.IntSLT, nil >>
	| "sle"   << ast.IntSLE, nil >>
;

FCmpInst
	: "fcmp" FloatPred Type Value "," Value   << astx.NewFCmpInst($1, $2, $3, $5) >>
;

FloatPred
	: "false"   << ast.FloatFalse, nil >>
	| "oeq"     << ast.FloatOEQ, nil >>
	| "ogt"     << ast.FloatOGT, nil >>
	| "oge"     << ast.FloatOGE, nil >>
	| "olt"     << ast.FloatOLT, nil >>
	| "ole"     << ast.FloatOLE, nil >>
	| "one"     << ast.FloatONE, nil >>
	| "ord"     << ast.FloatORD, nil >>
	| "ueq"     << ast.FloatUEQ, nil >>
	| "ugt"     << ast.FloatUGT, nil >>
	| "uge"     << ast.FloatUGE, nil >>
	| "ult"     << ast.FloatULT, nil >>
	| "ule"     << ast.FloatULE, nil >>
	| "une"     << ast.FloatUNE, nil >>
	| "uno"     << ast.FloatUNO, nil >>
	| "true"    << ast.FloatTrue, nil >>
;

PhiInst
	: "phi" Type IncomingList   << astx.NewPhiInst($1, $2) >>
;

IncomingList
	: Incoming                    << astx.NewIncomingList($0) >>
	| IncomingList "," Incoming   << astx.AppendIncoming($0, $2) >>
;

Incoming
	: "[" Value "," LocalIdent "]"   << astx.NewIncoming($1, $3) >>
;

SelectInst
	: "select" Type Value "," Type Value "," Type Value   << astx.NewSelectInst($1, $2, $4, $5, $7, $8) >>
;

CallInst
	: "call" ResultType GlobalIdent "(" Args ")"   << astx.NewCallInst($1, $2, $4) >>
	| "call" ResultType LocalIdent "(" Args ")"   << astx.NewCallInst($1, $2, $4) >>
;

Args
	: empty
	| ArgList
;

ArgList
	: Arg               << astx.NewValueList($0) >>
	| ArgList "," Arg   << astx.AppendValue($0, $2) >>
;

Arg
	: Type Value   << astx.NewValue($0, $1) >>
;

// === [ Terminators ] =========================================================

Terminator
	: RetTerm
	| BrTerm
	| CondBrTerm
	| SwitchTerm
	| UnreachableTerm
;

RetTerm
	: "ret" VoidType     << &ast.TermRet{}, nil >>
	| "ret" Type Value   << astx.NewRetTerm($1, $2) >>
;

BrTerm
	: "br" LabelType LocalIdent   << astx.NewBrTerm($1, $2) >>
;

CondBrTerm
	: "br" IntType Value "," LabelType LocalIdent "," LabelType LocalIdent   << astx.NewCondBrTerm($1, $2, $4, $5, $7, $8) >>
;

SwitchTerm
	: "switch" IntType Value "," LabelType LocalIdent "[" Cases "]"   << astx.NewSwitchTerm($1, $2, $4, $5, $7) >>
;

Cases
	: empty
	| CastList
;

CastList
	: Case            << astx.NewCaseList($0) >>
	| CastList Case   << astx.AppendCase($0, $1) >>
;

Case
	: IntType Value "," LabelType LocalIdent   << astx.NewCase($0, $1, $3, $4) >>
;

UnreachableTerm
	: "unreachable"   << &ast.TermUnreachable{}, nil >>
;
